

ext.genDir = "gen"

project.buildDir = "../${rootProject.ext.genDir}/$project.name"
println "setting root project $project.name build dir to: $project.buildDir"

project.ext {
  idePrefix = "${rootProject.buildDir}/../gradle-idea/"

  deployRepoUri = '../../shorn.bitbucket.org/maven'
  snapshotRepoUri = "${rootProject.buildDir}/../repo"
}

allprojects { Project iProject ->
  apply plugin: 'idea'
  apply plugin: 'maven'

  repositories {
    jcenter()
  }

//  idea.module.outputFile =
//    file("${rootProject.ext.idePrefix}/${iProject.name}.iml")
}

// This is replaced by the dog-fooding section at end of file
//idea {
//  tasks.ideaWorkspace.outputFile =
//    file("${rootProject.ext.idePrefix}/${rootProject.name}.iws")
//  project {
//    languageLevel = '1.6'
//    jdkName = '1.6'
//    outputFile = file("${rootProject.ext.idePrefix}/${rootProject.name}.ipr")
//  }
//}


subprojects { Project iProject ->
  apply plugin: 'groovy'

  iProject.buildDir = "$rootProject.buildDir/$iProject.name"

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  dependencies {
  }

}


project(':eclipsei2g') {
  group = 'g2extensions'
  version = '0.0.1-SNAPSHOT'

  dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.2'
    compile 'org.gradle:gradle-core:2.0'
  }
}

project(':g2idea13') {
  group = 'g2extensions'
  version = '0.0.1-SNAPSHOT'

  dependencies {
    compile gradleApi()

    // gradleApi() will bring all of the following (different version of groovy)
//    compile 'org.gradle:gradle-core:2.0'
//    compile 'org.gradle:gradle-ide:2.0'
//    compile 'org.codehaus.groovy:groovy-all:2.3.2'
  }
}


configure([project(':eclipsei2g'), project(':g2idea13')]) {

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourceJar
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: uri(rootProject.ext.deployRepoUri))
        snapshotRepository(url: uri(rootProject.ext.snapshotRepoUri))
      }
    }
  }

}


// Special dog-fooding bootstrap section, where I try to use a previously built
// version of g2idea13 to generate the idea project for g2extensions itself
// It's a bit crazy, comment out everything below this if you're getting
// problems from this stuff.

/**
 * Before you can run the "idea" task to generate an IDEA project, you have to
 * build g2extensions from the command line first.
 * Make sure the version that this tries to include is the same as what is
 * defined for the ':g2idea13' project you built.
 * Can't use do it dynamically because buildscript section is special.
 */
buildscript{
  if( System.properties.specialInclude ){
    repositories{
      jcenter()
      maven{
        url uri("../gen/repo")
      }
    }

    dependencies{
      classpath 'g2extensions:g2idea13:0.0.1-SNAPSHOT'
    }
  }
}

if( System.properties.specialInclude ){
  apply from: 'examples/g2extensions/build.gradle'
}
