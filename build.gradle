/**
 * If you want to actually run g2idea13 on the g2extensions project,
 * make sure you have previously built and uploaded a version to the snapshot
 * repo.
 * Then run the idea task with the "g2ideaidea13=true" system property set.
 * If you set this property on the very first run on a machine,
 * the build won't work.
 */
buildscript{
  // buildScript can't see variables in the rest of the script
  ext.g2idea13Version = '1.1.0-SNAPSHOT'
  ext.eclipsei2gVersion = '1.1.0-SNAPSHOT'

  repositories{
    jcenter()
    maven{
      url uri("../gen/repo")
    }
  }

  if( System.properties.g2idea13build ){
    dependencies{
      classpath 'g2extensions:g2idea13:' + g2idea13Version
    }
  }
}



ext.genDir = "gen"

project.buildDir = "../${rootProject.ext.genDir}/$project.name"
println "setting root project $project.name build dir to: $project.buildDir"

project.ext {
  idePrefix = "${rootProject.buildDir}/../gradle-idea/"

  deployRepoUri = "${rootProject.buildDir}/../deploy-repo"
  snapshotRepoUri = "${rootProject.buildDir}/../snapshot-repo"
}

allprojects { Project iProject ->
  apply plugin: 'idea'
  apply plugin: 'maven'

  repositories {
    jcenter()
  }

  if( System.properties.g2idea13build ){
    apply plugin: 'g2idea13'
  }

}

subprojects { Project iProject ->
  apply plugin: 'groovy'

  iProject.buildDir = "$rootProject.buildDir/$iProject.name"

  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}


project(':eclipsei2g') {
  group = 'g2extensions'
  version = eclipsei2gVersion

  dependencies {
    compile gradleApi()
  }
}

project(':g2idea13') {
  group = 'g2extensions'
  version = g2idea13Version

  dependencies {
    compile gradleApi()
  }
}


configure([project(':eclipsei2g'), project(':g2idea13')]) {

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourceJar
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: uri(rootProject.ext.deployRepoUri))
        snapshotRepository(url: uri(rootProject.ext.snapshotRepoUri))
      }
    }
  }

}

idea {
  if( System.properties.g2idea13build ){
    g2idea13{
//      baseDirectory = rootProject.ext.idePrefix + 'test/' // DO NOT COMMIT
      baseDirectory = rootProject.ext.idePrefix

      vcs = 'hg4idea'
      vcsDirectory = rootProject.projectDir
      finalMessage =
        "IDEA project files generated to ${file(project.ext.idePrefix)}"
    }
  }

  project {
    languageLevel = '1.6'
    jdkName = '1.6'
  }

  module{
    excludeDirs += file('gradle')
    excludeDirs += file('examples/eclipsei2g/.gradle')
    excludeDirs += file('examples/g2idea13/.gradle')
  }
}

