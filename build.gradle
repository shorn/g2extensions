/**
 * If you want to actually run g2idea13 on the g2extensions project, make you
 * have previously built and uploaded a version to the snapshot repo.
 * Then run the idea task with the "g2ideaidea13=true" system property set.
 * If you set this property on the very first run on a machine,
 * the build won't wprk.
 */
buildscript{
  // buildScript can't see variables in the rest of the script
  ext.g2idea13Version = '0.0.1-SNAPSHOT'

  repositories{
    jcenter()
    maven{
      url uri("../gen/repo")
    }
  }

  if( System.properties.g2idea13build ){
    dependencies{
      classpath 'g2extensions:g2idea13:' + project.ext.g2idea13Version
    }
  }
}



ext.genDir = "gen"

project.buildDir = "../${rootProject.ext.genDir}/$project.name"
println "setting root project $project.name build dir to: $project.buildDir"

project.ext {
  idePrefix = "${rootProject.buildDir}/../gradle-idea/"

  deployRepoUri = '../../shorn.bitbucket.org/maven'
  snapshotRepoUri = "${rootProject.buildDir}/../repo"
}

allprojects { Project iProject ->
  apply plugin: 'idea'
  apply plugin: 'maven'

  repositories {
    jcenter()
  }

  if( System.properties.g2idea13build ){
    apply plugin: 'g2idea13'
  }

}

subprojects { Project iProject ->
  apply plugin: 'groovy'

  iProject.buildDir = "$rootProject.buildDir/$iProject.name"

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

}


project(':eclipsei2g') {
  group = 'g2extensions'
  version = '0.0.1-SNAPSHOT'

  dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.2'
    compile 'org.gradle:gradle-core:2.0'
  }
}

project(':g2idea13') {
  group = 'g2extensions'
  version = g2idea13Version

  dependencies {
    compile gradleApi()

    // gradleApi() will bring all of the following (different version of groovy)
//    compile 'org.gradle:gradle-core:2.0'
//    compile 'org.gradle:gradle-ide:2.0'
//    compile 'org.codehaus.groovy:groovy-all:2.3.2'
  }
}


configure([project(':eclipsei2g'), project(':g2idea13')]) {

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourceJar
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: uri(rootProject.ext.deployRepoUri))
        snapshotRepository(url: uri(rootProject.ext.snapshotRepoUri))
      }
    }
  }

}

idea {
  if( System.properties.g2idea13build ){
    g2idea13{
      baseDirectory = rootProject.ext.idePrefix + 'test/' // DO NOT COMMIT
//      baseDirectory = rootProject.ext.idePrefix

      vcs = 'hg4idea'
      vcsDirectory = rootProject.projectDir
    }
  }

  project {
    languageLevel = '1.6'
    jdkName = '1.6'
  }

  module{
    excludeDirs += file('examples/eclipsei2g/.gradle"')
    excludeDirs += file('examples/eclipsei2g/build"')
    excludeDirs += file('examples/g2idea13/.gradle"')
    excludeDirs += file('examples/g2idea13/build')
    excludeDirs += file('gradle"')
  }
}

