ext.genDir = "gen"

project.buildDir = "../${rootProject.ext.genDir}/$project.name"
println "setting root project $project.name build dir to: $project.buildDir"

project.ext {
  idePrefix = "${rootProject.buildDir}/../gradle-idea/"

  deployRepoUri = '../../shorn.bitbucket.org/maven'
  snapshotRepoUri = "${rootProject.buildDir}/../repo"
}

allprojects { Project iProject ->
  apply plugin: 'idea'
  apply plugin: 'maven'

  repositories {
    jcenter()
  }

  idea.module.outputFile =
    file("${rootProject.ext.idePrefix}/${iProject.name}.iml")
}

idea {
  tasks.ideaWorkspace.outputFile =
    file("${rootProject.ext.idePrefix}/${rootProject.name}.iws")
  project {
    languageLevel = '1.6'
    jdkName = '1.6'
    outputFile = file("${rootProject.ext.idePrefix}/${rootProject.name}.ipr")
  }
}


subprojects { Project iProject ->
  apply plugin: 'groovy'

  iProject.buildDir = "$rootProject.buildDir/$iProject.name"

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.2'
  }

}


project(':eclipsei2g') {
  group = 'eclipsei2g'
  version = '0.0.1-SNAPSHOT'

  dependencies {
    compile 'org.gradle:gradle-core:2.0'
  }
}

project(':g2idea13') {
  group = 'g2idea13'
  version = '0.0.1-SNAPSHOT'

  dependencies {
    compile gradleApi()
//    compile 'org.gradle:gradle-core:2.0'
//    compile 'org.gradle:gradle-ide:2.0'
  }
}


configure([project(':eclipsei2g'), project(':g2idea13')]) {

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourceJar
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: uri(rootProject.ext.deployRepoUri))
        snapshotRepository(url: uri(rootProject.ext.snapshotRepoUri))
      }
    }
  }

}
